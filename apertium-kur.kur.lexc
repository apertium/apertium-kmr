Multichar_Symbols

%<v%> %<n%> %<pr%> %<cnjcoo%> %<cnjsub%> %<prn%> %<adj%>
%<pri%> %<prs%> %<past%> %<inf%> %<neg%> %<pii%> %<pp%> %<imp%> %<perf%> %<pres%> %<pis%> %<cond%> 
%<p1%> %<p2%> %<p3%>
%<m%> %<f%>
%<sg%> %<pl%>
%<ind%> %<def%> %<rel%> %<dem%> %<pers%>
%<nom%> %<obl%> %<con%> %<voc%>
%<pst%> %<comp%> %<sup%>
%<sent%>

%{PREF%} %{VSUF%} %{VSUFP%} %{EPREF%} %{IEPH%}

@P.PREF.di@ @P.PREF.bi@ @P.PREF.na@ @P.PREF.ne@ @P.PREF.nedi@
@R.PREF.bi@ @R.PREF.di@ @R.PREF.na@ @R.PREF.ne@ @R.PREF.nedi@
@D.PREF@ @D.PREF.ne@

%>

LEXICON Root

VerbRoot ;
NounRoot ;
AdjRoot ;
PronRoot ;
Prepositions ;
Conjunctions ;
Numerals ;
Punctuation ;


LEXICON PronRoot 

ez%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:ez # ;
ez%<prn%>%<pers%>%<p1%>%<sg%>%<obl%>:min # ;
tu%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:tu # ;
tu%<prn%>%<pers%>%<p2%>%<sg%>%<obl%>:te # ;

em%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:em # ;
em%<prn%>%<pers%>%<p1%>%<pl%>%<obl%>:me # ;
hun%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:hun # ;
hun%<prn%>%<pers%>%<p2%>%<pl%>%<obl%>:we # ;

ew%<prn%>%<dem%>%<sg%>%<nom%>:ew # ;
ew%<prn%>%<dem%>%<m%>%<sg%>%<obl%>:wî # ;
ew%<prn%>%<dem%>%<f%>%<sg%>%<obl%>:wê # ;
ew%<prn%>%<dem%>%<pl%>%<nom%>:ew # ;
ew%<prn%>%<dem%>%<pl%>%<obl%>:wan # ;

ev%<prn%>%<dem%>%<sg%>%<nom%>:ev # ;
ev%<prn%>%<dem%>%<m%>%<sg%>%<obl%>:vî # ;
ev%<prn%>%<dem%>%<f%>%<sg%>%<obl%>:vê # ;
ev%<prn%>%<dem%>%<pl%>%<nom%>:ev # ;
ev%<prn%>%<dem%>%<pl%>%<obl%>:van # ;

LEXICON yek__num

%<num%>%<nom%>%<ind%>:%>ek # ;
%<num%>%<obl%>%<ind%>:%>ekê # ;
%<num%>%<con%>%<ind%>:%>eke # ;
%<num%>%<nom%>%<def%>: # ;
%<num%>%<obl%>%<def%>:%>ê # ;
%<num%>%<con%>%<def%>:%>a # ;

LEXICON şeş__num

%<num%>%<nom%>%<ind%>: # ;
%<num%>%<obl%>%<ind%>: # ;
%<num%>%<con%>%<ind%>: # ;
%<num%>%<nom%>%<def%>: # ;
%<num%>%<obl%>%<def%>: # ;
%<num%>%<con%>%<def%>: # ;



LEXICON Prep

%<pr%>: # ;

LEXICON CC 

%<cnjcoo%>: # ;

LEXICON CS

%<cnjsub%>: # ;


LEXICON nû__adj

%<adj%>%<pst%>:%> #;
%<adj%>%<comp%>:%>tir # ;
%<adj%>%<sup%>:%>tirîn # ;

LEXICON pêwis/t__adj

%<adj%>%<pst%>:%>t #;
%<adj%>%<comp%>:%>tir # ;
%<adj%>%<sup%>:%>tirîn # ;


LEXICON mirov__n

%<n%>%<m%>%<sg%>%<nom%>%<ind%>:%>ek # ;
%<n%>%<m%>%<sg%>%<obl%>%<ind%>:%>ekî # ;
%<n%>%<m%>%<sg%>%<con%>%<ind%>:%>ekî # ;
%<n%>%<m%>%<sg%>%<nom%>%<def%>: # ;
%<n%>%<m%>%<sg%>%<obl%>%<def%>: # ;
%<n%>%<m%>%<sg%>%<con%>%<def%>:%>ê # ;
%<n%>%<m%>%<sg%>%<voc%>%<def%>:%>o # ;
%<n%>%<m%>%<pl%>%<nom%>%<ind%>:%>in # ;
%<n%>%<m%>%<pl%>%<obl%>%<ind%>:%>inan # ;
%<n%>%<m%>%<pl%>%<con%>%<ind%>:%>ine # ;
%<n%>%<m%>%<pl%>%<nom%>%<def%>: # ;
%<n%>%<m%>%<pl%>%<obl%>%<def%>:%>an # ;
%<n%>%<m%>%<pl%>%<obl%>%<def%>:%>a # ; ! ^LR^
%<n%>%<m%>%<pl%>%<con%>%<def%>:%>ên # ;
%<n%>%<m%>%<pl%>%<voc%>%<def%>:%>ên # ;

LEXICON ba__n 

%<n%>%<m%>%<sg%>%<nom%>%<ind%>:%>yek # ;
%<n%>%<m%>%<sg%>%<obl%>%<ind%>:%>yekî # ;
%<n%>%<m%>%<sg%>%<con%>%<ind%>:%>yekî # ;
%<n%>%<m%>%<sg%>%<nom%>%<def%>: # ;
%<n%>%<m%>%<sg%>%<obl%>%<def%>: # ;
%<n%>%<m%>%<sg%>%<con%>%<def%>:%>yê # ;
%<n%>%<m%>%<sg%>%<voc%>%<def%>:%>yo # ;
%<n%>%<m%>%<pl%>%<nom%>%<ind%>:%>yin # ;
%<n%>%<m%>%<pl%>%<obl%>%<ind%>:%>yinan # ;
%<n%>%<m%>%<pl%>%<con%>%<ind%>:%>yine # ;
%<n%>%<m%>%<pl%>%<nom%>%<def%>: # ;
%<n%>%<m%>%<pl%>%<obl%>%<def%>:%>yan # ;
%<n%>%<m%>%<pl%>%<con%>%<def%>:%>yên # ;
%<n%>%<m%>%<pl%>%<voc%>%<def%>:%>no # ;

LEXICON xan/î__n

%<n%>%<m%>%<sg%>%<nom%>%<ind%>:%>iyek # ;
%<n%>%<m%>%<sg%>%<obl%>%<ind%>:%>iyekî # ;
%<n%>%<m%>%<sg%>%<con%>%<ind%>:%>iyekî # ;
%<n%>%<m%>%<sg%>%<nom%>%<def%>:%>î # ;
%<n%>%<m%>%<sg%>%<obl%>%<def%>:%>î # ;
%<n%>%<m%>%<sg%>%<con%>%<def%>:%>iyê # ;
%<n%>%<m%>%<sg%>%<voc%>%<def%>:%>o # ;
%<n%>%<m%>%<pl%>%<nom%>%<ind%>:%>iyin # ;
%<n%>%<m%>%<pl%>%<obl%>%<ind%>:%>iyinan # ;
%<n%>%<m%>%<pl%>%<con%>%<ind%>:%>iyine # ;
%<n%>%<m%>%<pl%>%<nom%>%<def%>:%>î # ;
%<n%>%<m%>%<pl%>%<obl%>%<def%>:%>iyan # ;
%<n%>%<m%>%<pl%>%<con%>%<def%>:%>iyên # ;
%<n%>%<m%>%<pl%>%<voc%>%<def%>:%>iyên # ;

LEXICON roj__n

%<n%>%<f%>%<sg%>%<nom%>%<ind%>:%>ek # ;
%<n%>%<f%>%<sg%>%<obl%>%<ind%>:%>ekê # ;
%<n%>%<f%>%<sg%>%<con%>%<ind%>:%>eke # ;
%<n%>%<f%>%<sg%>%<nom%>%<def%>: # ;
%<n%>%<f%>%<sg%>%<obl%>%<def%>:%>ê # ;
%<n%>%<f%>%<sg%>%<con%>%<def%>:%>a # ;
%<n%>%<f%>%<sg%>%<voc%>%<def%>:%>ê # ;
%<n%>%<f%>%<pl%>%<nom%>%<ind%>:%>in # ;
%<n%>%<f%>%<pl%>%<obl%>%<ind%>:%>inan # ;
%<n%>%<f%>%<pl%>%<con%>%<ind%>:%>ine # ;
%<n%>%<f%>%<pl%>%<nom%>%<def%>: # ;
%<n%>%<f%>%<pl%>%<obl%>%<def%>:%>ên # ;
%<n%>%<f%>%<pl%>%<con%>%<def%>:%>an # ;
%<n%>%<f%>%<pl%>%<voc%>%<def%>:%>ên # ;




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! VERBS
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! Verb forms in Kurdish are usually formed from a prefix indicating mood, a 
! stem (either present or past) and a personal suffix. For example:
!
! 'Ez diçim' (< 'çûn')
!     di- ç          -im
!     IND read.PRES  P1.SG
! 'I read' 
!
! But if the verb is a compound, the prefix attaches to the verbal part:
!
! 'Ez vedixwim' (< 'vexwarin')
!     ve-  di- xw       -im
!     PREF IND eat.PRES P1.SG
! 'I drink'
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prefixes

!!!   To form the present tense, the stressed progressive/habitual modal marker
!!! di- is prefixed to the present stem, and a suffixed personal ending is added 
!!! to the stem.

@P.PREF.di@:di%{PREF%}@P.PREF.di@ VerbStems ;
@P.PREF.bi@:bi%{PREF%}@P.PREF.bi@ VerbStems ;
@P.PREF.na@:na%{PREF%}@P.PREF.na@ VerbStems ;
@P.PREF.ne@:ne%{PREF%}@P.PREF.ne@ VerbStems ;
@P.PREF.nedi@:nedi%{PREF%}@P.PREF.nedi@ VerbStems ; !! Negative past imperfect 

!!! If the present stem begins with a- or ê-, the prefix di- may drop its vowel
!!! and becomes d-, as in axaftin/axiv- ‘speak’ and êşandin/êşîn- ‘to hurt,’ the
!!! present tenses of which are either daxivim or diaxivim ‘I speak’ and either
!!! dêşînim or diêşînim ‘I hurt.’

@P.PREF.di@:d%{EPREF%}@P.PREF.di@ VerbStems ;

LEXICON VerbRoot

Prefixes ;
VerbStems ;
IrregularVerbs; 
CompoundVerbs; 

LEXICON IrregularVerbs

bûn%<v%>%<inf%>:bûn # ;
bûn%<v%>%<pri%>: PersonalSuffixes ; ! copula

!   The combination of an oblique plural ending in -an and 
!   the first-person singular enclitic im usually 
!   becomes -a me instead of -an im.
bûn%<v%>%<pri%>%<p1%>%<sg%>:me # ; 


!   There are two present subjunctives of the verb bûn ‘to be, become,’ with
!   and without the subjunctive marker bi-. The conjugation without the marker
!   is used when the verb means ‘be’; the conjugation with the marker is used
!   when the verb means ‘become’ and when it is part of a compound verb.

bûn%<v%>%<neg%>%<prs%>%<p1%>%<sg%>:nebim # ; 
bûn%<v%>%<neg%>%<prs%>%<p2%>%<sg%>:nebî # ;
bûn%<v%>%<neg%>%<prs%>%<p3%>%<sg%>:nebe # ;
bûn%<v%>%<neg%>%<prs%>%<p1%>%<pl%>:nebin # ;
bûn%<v%>%<neg%>%<prs%>%<p2%>%<pl%>:nebin # ;
bûn%<v%>%<neg%>%<prs%>%<p3%>%<pl%>:nebin # ;

bûn%<v%>%<prs%>: BûnPrs ;
bûn%<v%>%<prs%>:bi BûnPrs ;
bûn%<v%>%<past%>: BûnPast ;
bûn%<v%>%<past%>%<cond%>@D.PREF@:bû@D.PREF@ PersonalSuffixesIrrealis ; ! ez bûma
bûn%<v%>%<past%>%<cond%>@P.PREF.bi@:bibû@P.PREF.bi@ PersonalSuffixesIrrealis ; ! ez bibûma

bûn%<v%>%<neg%>%<past%>%<p1%>%<sg%>:nebûm # ; 
bûn%<v%>%<neg%>%<past%>%<p2%>%<sg%>:nebûyî # ;
bûn%<v%>%<neg%>%<past%>%<p3%>%<sg%>:nebû # ;
bûn%<v%>%<neg%>%<past%>%<p1%>%<pl%>:nebûn # ;
bûn%<v%>%<neg%>%<past%>%<p2%>%<pl%>:nebûn # ;
bûn%<v%>%<neg%>%<past%>%<p3%>%<pl%>:nebûn # ;
bûn%<v%>%<neg%>%<pii%>%<p1%>%<sg%>:nedibûm # ; 
bûn%<v%>%<neg%>%<pii%>%<p2%>%<sg%>:nedibûyî # ;
bûn%<v%>%<neg%>%<pii%>%<p3%>%<sg%>:nedibû # ;
bûn%<v%>%<neg%>%<pii%>%<p1%>%<pl%>:nedibûn # ;
bûn%<v%>%<neg%>%<pii%>%<p2%>%<pl%>:nedibûn # ;
bûn%<v%>%<neg%>%<pii%>%<p3%>%<pl%>:nedibûn # ;

hebûn%<v%>%<inf%>:hebûn # ;
hebûn%<v%>%<pri%>%<p1%>%<sg%>:heme # ;
hebûn%<v%>%<pri%>%<p2%>%<sg%>:heyî # ;
hebûn%<v%>%<pri%>%<p3%>%<sg%>:heye # ;
hebûn%<v%>%<pri%>%<p1%>%<pl%>:hene # ;
hebûn%<v%>%<pri%>%<p2%>%<pl%>:hene # ;
hebûn%<v%>%<pri%>%<p3%>%<pl%>:hene # ;
hebûn%<v%>%<past%>%<p1%>%<sg%>:hebûm # ;
hebûn%<v%>%<past%>%<p2%>%<sg%>:hebûyî # ;
hebûn%<v%>%<past%>%<p3%>%<sg%>:hebû # ;
hebûn%<v%>%<past%>%<p1%>%<pl%>:hebûn # ;
hebûn%<v%>%<past%>%<p2%>%<pl%>:hebûn # ;
hebûn%<v%>%<past%>%<p3%>%<pl%>:hebûn # ;
hebûn%<v%>%<neg%>%<pri%>%<p3%>%<sg%>:tune # ;
hebûn%<v%>%<neg%>%<past%>%<p3%>%<sg%>:tunebû # ;

! There are two present subjunctives of hatin ‘to come.’ One is regularly
! conjugated based on the stem wer- without the bi- prefix; the other is regu-
! larly conjugated based on the stem bê-, a contraction of bi- and yê-, the
! present stem of hatin.

hebûn%<v%>%<prs%>%<p1%>%<sg%>:werim # ;
hebûn%<v%>%<prs%>%<p2%>%<sg%>:werî # ;
hebûn%<v%>%<prs%>%<p3%>%<sg%>:were # ;
hebûn%<v%>%<prs%>%<p1%>%<pl%>:werin # ;
hebûn%<v%>%<prs%>%<p2%>%<pl%>:werin # ;
hebûn%<v%>%<prs%>%<p3%>%<pl%>:werin # ;
hebûn%<v%>%<prs%>%<p1%>%<sg%>:bêm # ;
hebûn%<v%>%<prs%>%<p2%>%<sg%>:bêyî # ;
hebûn%<v%>%<prs%>%<p3%>%<sg%>:bê # ;
hebûn%<v%>%<prs%>%<p1%>%<pl%>:bên # ;
hebûn%<v%>%<prs%>%<p2%>%<pl%>:bên # ;
hebûn%<v%>%<prs%>%<p3%>%<pl%>:bên # ;
hebûn%<v%>%<neg%>%<prs%>%<p1%>%<sg%>:newerim # ;
hebûn%<v%>%<neg%>%<prs%>%<p2%>%<sg%>:newerî # ;
hebûn%<v%>%<neg%>%<prs%>%<p3%>%<sg%>:newere # ;
hebûn%<v%>%<neg%>%<prs%>%<p1%>%<pl%>:newerin # ;
hebûn%<v%>%<neg%>%<prs%>%<p2%>%<pl%>:newerin # ;
hebûn%<v%>%<neg%>%<prs%>%<p3%>%<pl%>:newerin # ;
hebûn%<v%>%<neg%>%<prs%>%<p1%>%<sg%>:neyêm # ;
hebûn%<v%>%<neg%>%<prs%>%<p2%>%<sg%>:neyêyî # ;
hebûn%<v%>%<neg%>%<prs%>%<p3%>%<sg%>:neyê # ;
hebûn%<v%>%<neg%>%<prs%>%<p1%>%<pl%>:neyên # ;
hebûn%<v%>%<neg%>%<prs%>%<p2%>%<pl%>:neyên # ;
hebûn%<v%>%<neg%>%<prs%>%<p3%>%<pl%>:neyên # ;

!!!    There are two verbs with irregular conjugations in the present tense, 
!!!    hatin ‘to come’ and anîn ‘to bring.’ In both these verbs the dí- modal 
!!!    marker has assimilated to the present stem and appears as t-. 

@D.PREF@hat:hat@D.PREF@ Accidents_PastStem_in ; ! lm="hatin" :: "to come"
@P.PREF.di@hat:tê@P.PREF.di@ Accidents_PresStem_in ; ! ez têm
@P.PREF.bi@hat:bê@P.PREF.bi@ Accidents_PresStem_in ; ! ez bêm
@P.PREF.na@hat:nayê@P.PREF.na@ Accidents_PresStem_in ; ! ez nayêm
@P.PREF.ne@hat:nehat@P.PREF.ne@ Accidents_PastStem_in ; ! ez nehatim
@P.PREF.nedi@hat:nedihat@P.PREF.nedi@ Accidents_PastStem_in ; ! ez nedihatim

@D.PREF@an:anî@D.PREF@ Accidents_PastStem_în ; ! lm="anîn" :: "to bring"
@P.PREF.di@an:tîn@P.PREF.di@ Accidents_PresStem_în ; ! ez tînim
@P.PREF.bi@an:bîn@P.PREF.bi@ Accidents_PresStem_în ; ! ez bînim
@P.PREF.na@an:nayn@P.PREF.na@ Accidents_PresStem_în ; ! ez naynim
@P.PREF.ne@an:neanîn@P.PREF.ne@ Accidents_PastStem_în ; ! ez neanînim
@P.PREF.nedi@an:nedianîn@P.PREF.nedi@ Accidents_PastStem_în ; ! ez nedianîntim

!!!    There are also two verbs, zanîn ‘to know’ and karîn ‘to be able,’ 
!!!    that are irregular in the negative. They form the negative with ní i
!!!    instead of na.

@D.PREF@zan:zanî@D.PREF@ Accidents_PastStem_în ; ! lm="zanîn" :: "to know"
@P.PREF.di@zan:dizan@P.PREF.di@ Accidents_PresStem_în ; ! ez dizanim
@P.PREF.bi@zan:bizan@P.PREF.bi@ Accidents_PresStem_în ; ! ez bizanim
@P.PREF.na@zan:nizan@P.PREF.na@ Accidents_PresStem_în ; ! ez nizanim
@P.PREF.ne@zan:nezan@P.PREF.ne@ Accidents_PastStem_în ; ! ez nezanim
@P.PREF.nedi@zan:nedizan@P.PREF.nedi@ Accidents_PastStem_în ; ! ez nedizanim
@P.PREF.bi@zanîn%<v%>%<pis%>:bizani%{IEPH%}@P.PREF.bi@ BûnPrs ; ! ez bizanibim
!! the bí- subjunctive prefix may be omitted for stylisic reasons
@D.PREF@zanîn%<v%>%<pis%>:zani%{IEPH%}@D.PREF@ BûnPrs ; ! ez zanibim ^LR^

@D.PREF@kar:karî@D.PREF@ Accidents_PastStem_în ; ! lm="karîn" :: "to be able to"
@P.PREF.di@kar:dikar@P.PREF.di@ Accidents_PresStem_în ; ! ez dikarim
@P.PREF.bi@kar:bikar@P.PREF.bi@ Accidents_PresStem_în ; ! ez bikarim
@P.PREF.na@kar:nikar@P.PREF.na@ Accidents_PresStem_în ; ! ez nikarim
@P.PREF.ne@kar:nekar@P.PREF.ne@ Accidents_PastStem_în ; ! ez nekarim
@P.PREF.nedi@kar:nedikar@P.PREF.nedi@ Accidents_PastStem_în ; ! ez nedikarim
@P.PREF.bi@karîn%<v%>%<pis%>:bikari%{IEPH%}@P.PREF.bi@ BûnPrs ; ! ez bikaribim 
!! the bí- subjunctive prefix may be omitted for stylisic reasons
@D.PREF@karîn%<v%>%<pis%>:kari%{IEPH%}@D.PREF@ BûnPrs ; ! ez karibim ^LR^

! There are also two present subjunctives of çûn ‘to go.’ One is regularly
! congujated based on the stem ç- with the bí- subjunctive marker; the other
! is regularly conjugated based on the stem her- without the subjunctive
! marker.

@P.PREF.bi@ç:her@P.PREF.bi@ Accidents_PresStem_ûn  ; ! lm="çûn" :: "to go"
@P.PREF.ne@ç:neher@P.PREF.ne@ Accidents_PresStem_ûn  ; ! lm="çûn" :: "to go"

VerbStems;

LEXICON BûnPast

!
! We keep this in a separate sublexicon as it is used for forming the 
! PastPerf. PastPerf = past stem + i + past form of 'bûn'
!

%<p1%>%<sg%>:bûm # ; 
%<p2%>%<sg%>:bûyî # ;
%<p3%>%<sg%>:bû # ;
%<p1%>%<pl%>:bûn # ;
%<p2%>%<pl%>:bûn # ;
%<p3%>%<pl%>:bûn # ;

LEXICON BûnPrs 

! 
! Separate sublexicon for past subjunctive, 
!  Pis = past stem + i + prs of 'bûn'
!

%<p1%>%<sg%>:bim # ;
%<p2%>%<sg%>:bî # ;
%<p3%>%<sg%>:be # ;
%<p1%>%<pl%>:bin # ;
%<p2%>%<pl%>:bin # ;
%<p3%>%<pl%>:bin # ;

LEXICON CompoundVerbs

VerbCompound_ve ;
VerbCompound_hil ;
VerbCompound_ra ;
VerbCompound_rû ;

LEXICON VerbCompound_ve

ve@D.PREF@:ve@D.PREF@ CompoundStems_ve ;
ve@P.PREF.di@:vedi%{PREF%}@P.PREF.di@ CompoundStems_ve ;
ve@P.PREF.na@:vena%{PREF%}@P.PREF.na@ CompoundStems_ve ;
ve@P.PREF.ne@:vene%{PREF%}@P.PREF.ne@ CompoundStems_ve ;
ve@P.PREF.nedi@:venedi%{PREF%}@P.PREF.nedi@ CompoundStems_ve ; 

!! When the bí- prefix is omitted, the absence of a modal
!! modal prefix identifies the verb as subjunctive, as in 
!! ve-xwarin (ve-xw-) ‘to drink’

ve@P.PREF.bi@:ve%{PREF%}@P.PREF.bi@ CompoundStems_ve ; 

LEXICON VerbCompound_rû

rû@D.PREF@:rû@D.PREF@ CompoundStems_rû ;
rû@P.PREF.di@:rûdi%{PREF%}@P.PREF.di@ CompoundStems_rû ;
rû@P.PREF.na@:rûna%{PREF%}@P.PREF.na@ CompoundStems_rû ;
rû@P.PREF.ne@:rûne%{PREF%}@P.PREF.ne@ CompoundStems_rû ;
rû@P.PREF.nedi@:rûnedi%{PREF%}@P.PREF.nedi@ CompoundStems_rû ; 

!! When the bí- prefix is omitted, the absence of a modal
!! modal prefix identifies the rûrb as subjunctirû, as in 
!! rû-xwarin (rû-xw-) ‘to drink’

rû@P.PREF.bi@:rû%{PREF%}@P.PREF.bi@ CompoundStems_rû ; 

LEXICON VerbCompound_ra

ra@D.PREF@:ra@D.PREF@ CompoundStems_ra ;
ra@P.PREF.di@:radi%{PREF%}@P.PREF.di@ CompoundStems_ra ;
ra@P.PREF.na@:rana%{PREF%}@P.PREF.na@ CompoundStems_ra ;
ra@P.PREF.ne@:rane%{PREF%}@P.PREF.ne@ CompoundStems_ra ;
ra@P.PREF.nedi@:ranedi%{PREF%}@P.PREF.nedi@ CompoundStems_ra ; 

ra@P.PREF.bi@:ra%{PREF%}@P.PREF.bi@ CompoundStems_ra ; 

LEXICON VerbCompound_hil

hil@D.PREF@:hil@D.PREF@ CompoundStems_hil ;
hil@P.PREF.di@:hildi%{PREF%}@P.PREF.di@ CompoundStems_hil ;
hil@P.PREF.na@:hilna%{PREF%}@P.PREF.na@ CompoundStems_hil ;
hil@P.PREF.ne@:hilne%{PREF%}@P.PREF.ne@ CompoundStems_hil ;
hil@P.PREF.nedi@:hilnedi%{PREF%}@P.PREF.nedi@ CompoundStems_hil ; 

hil@P.PREF.bi@:hil%{PREF%}@P.PREF.bi@ CompoundStems_hil ; 

LEXICON CompoundStems_ve 

xwar:xw Accidents_PresStem_in ; ! lm="vexwarin" :: "to drink"
xwar:xwar Accidents_PastStem_in ; ! lm="vexwarin" :: "to drink"

kir:k Accidents_PresStem_in ; ! lm="vekirin" :: "to open"
kir:kir Accidents_PastStem_in ; ! lm="vekirin" :: "to open"

got:bêj Accidents_PresStem_in ; ! lm="vegotin" :: "to report"
got:got Accidents_PastStem_in ; ! lm="vegotin" :: "to report"

LEXICON CompoundStems_ra

bûn:b Accidents_PresStem_ûn  ; ! lm="rabûn" :: "to get up" 
bûn:b Accidents_PastStem_ûn; ! lm="rabûn" :: "to get up" 

ket:kev Accidents_PresStem_in  ; ! lm="raketin" :: "to sleep" 
ket:ket Accidents_PastStem_in  ; ! lm="raketin" :: "to sleep" 

kir:k Accidents_PresStem_in  ; ! lm="rakirin" :: "to lift up" 
kir:kir Accidents_PastStem_in  ; ! lm="rakirin" :: "to lift up" 

LEXICON CompoundStems_hil

!hilat:hilê; ! lm="hilat" :: "to get up" :: hil-ê- / hil-hat-
!dan:d ; ! lm="hildan" :: "to lift"

bijart:bijêr Accidents_PresStem_in ; ! lm="hilbijartin" :: "to choose" 
bijart:bijart Accidents_PastStem_in ; ! lm="hilbijartin" :: "to choose" 

LEXICON CompoundStems_rû

nişt:nê Accidents_PresStem_in ; ! lm="rûniştin" :: "to sit down"
nişt:nişt Accidents_PastStem_in ; ! lm="rûniştin" :: "to sit down"

!behs% kir:behs% kir ; ! lm="behs kirin" :: "to discuss"
!alîkarî% kir:alîkarî% kir ; ! lm="alîkarî kirin" :: "to help"

LEXICON Accidents_PresStem_ûn

ûn%<v%>%<pri%>@R.PREF.di@@D.PREF.ne@:@R.PREF.di@@D.PREF.ne@ PersonalSuffixes ; ! ez diçim
ûn%<v%>%<prs%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixes ; ! ez biçim
ûn%<v%>%<neg%>%<pri%>@R.PREF.na@:@R.PREF.na@ PersonalSuffixes ; ! ez naçim
ûn%<v%>%<neg%>%<prs%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez neçim
ûn%<v%>%<imp%>%<p2%>%<sg%>@R.PREF.bi@:e@R.PREF.bi@ # ; ! biçe
ûn%<v%>%<imp%>%<p2%>%<pl%>@R.PREF.bi@:in@R.PREF.bi@ # ; ! biçin
ûn%<v%>%<neg%>%<imp%>%<p2%>%<sg%>@R.PREF.ne@:e@R.PREF.ne@ # ; ! neke
ûn%<v%>%<neg%>%<imp%>%<p2%>%<pl%>@R.PREF.ne@:in@R.PREF.ne@ # ; ! nekin

LEXICON Accidents_PastStem_ûn

ûn%<v%>%<inf%>@D.PREF@:n@D.PREF@ # ; ! çûn
ûn%<v%>%<inf%>%<obl%>@D.PREF@:na@D.PREF@ # ; ! çûna
ûn%<v%>%<pp%>@D.PREF@:%{VSUFP%}î@D.PREF@ # ; ! çûnî

ûn%<v%>%<perf%>%<pres%>@D.PREF@:@D.PREF@ PersonalSuffixesPerfect ; ! ez çûme
ûn%<v%>%<neg%>%<perf%>%<pres%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixesPerfect ; ! ez neçûme

ûn%<v%>%<perf%>%<past%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPast ; ! ez çûbûm
ûn%<v%>%<neg%>%<perf%>%<past%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPast ; ! ez neçûbûm
ûn%<v%>%<pis%>@R.PREF.bi@:i%{IEPH%}@R.PREF.bi@ BûnPrs ; ! ez biçûbim
!! the bí- subjunctive prefix may be omitted for stylisic reasons
ûn%<v%>%<pis%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPrs ; ! ez biçûbim ^LR^
ûn%<v%>%<neg%>%<pis%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPrs ; ! ez neçûbim

ûn%<v%>%<past%>%<cond%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixesIrrealis ; ! ez biçûma

ûn%<v%>%<past%>@D.PREF@:@D.PREF@ PersonalSuffixes ; ! ez çûm
ûn%<v%>%<neg%>%<past%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez neçûm
ûn%<v%>%<pii%>@R.PREF.di@:@R.PREF.di@ PersonalSuffixes ; ! ez diçûm
ûn%<v%>%<neg%>%<pii%>@R.PREF.nedi@:@R.PREF.nedi@ PersonalSuffixes ; ! ez nediçûm

LEXICON Accidents_PresStem_in

in%<v%>%<pri%>@R.PREF.di@@D.PREF.ne@:@R.PREF.di@@D.PREF.ne@ PersonalSuffixes ; ! ez dikim
in%<v%>%<prs%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixes ; ! ez bikim
in%<v%>%<neg%>%<pri%>@R.PREF.na@:@R.PREF.na@ PersonalSuffixes ; ! ez nakim
in%<v%>%<neg%>%<prs%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nekim
in%<v%>%<imp%>%<p2%>%<sg%>@R.PREF.bi@:e@R.PREF.bi@ # ; ! bike
in%<v%>%<imp%>%<p2%>%<pl%>@R.PREF.bi@:in@R.PREF.bi@ # ; ! bikin
in%<v%>%<neg%>%<imp%>%<p2%>%<sg%>@R.PREF.ne@:e@R.PREF.ne@ # ; ! neke
in%<v%>%<neg%>%<imp%>%<p2%>%<pl%>@R.PREF.ne@:in@R.PREF.ne@ # ; ! nekin

LEXICON Accidents_PastStem_in

in%<v%>%<inf%>@D.PREF@:in@D.PREF@ # ; ! kirin
in%<v%>%<inf%>%<obl%>@D.PREF@:na@D.PREF@ # ; ! kirina
in%<v%>%<pp%>@D.PREF@:%{VSUFP%}î@D.PREF@ # ; ! kirî

in%<v%>%<perf%>%<pres%>@D.PREF@:@D.PREF@ PersonalSuffixesPerfect ; ! ez kirime 
in%<v%>%<neg%>%<perf%>%<pres%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixesPerfect ; ! ez nekirime

in%<v%>%<perf%>%<past%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPast ; ! ez kiribûm 
in%<v%>%<neg%>%<perf%>%<past%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPast ; ! ez nekiribûm
in%<v%>%<pis%>@R.PREF.bi@:i%{IEPH%}@R.PREF.bi@ BûnPrs ; ! ez bikiribim 
!! the bí- subjunctive prefix may be omitted for stylisic reasons
in%<v%>%<pis%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPrs ; ! ez kiribim ^LR^
in%<v%>%<neg%>%<pis%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPrs ; ! ez nekiribim

in%<v%>%<past%>%<cond%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixesIrrealis ; ! ez bikirama

in%<v%>%<past%>@D.PREF@:@D.PREF@ PersonalSuffixes ; ! ez kirim
in%<v%>%<neg%>%<past%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nekirim
in%<v%>%<pii%>@R.PREF.di@:@R.PREF.di@ PersonalSuffixes ; ! ez dikirim
in%<v%>%<neg%>%<pii%>@R.PREF.nedi@:@R.PREF.nedi@ PersonalSuffixes ; ! ez nedikirim

LEXICON Accidents_PresStem_în

în%<v%>%<pri%>@R.PREF.di@@D.PREF.ne@:@R.PREF.di@@D.PREF.ne@ PersonalSuffixes ; ! ez dinivîsim 
în%<v%>%<prs%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixes ; ! ez binivîsim
în%<v%>%<neg%>%<pri%>@R.PREF.na@:@R.PREF.na@ PersonalSuffixes ; ! ez nanivîsim
în%<v%>%<neg%>%<prs%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nenivîsim
în%<v%>%<imp%>%<p2%>%<sg%>@R.PREF.bi@:e@R.PREF.bi@ # ; ! binivîse
în%<v%>%<imp%>%<p2%>%<pl%>@R.PREF.bi@:in@R.PREF.bi@ # ; ! binivîsin
în%<v%>%<neg%>%<imp%>%<p2%>%<sg%>@R.PREF.ne@:e@R.PREF.ne@ # ; ! nenivîse
în%<v%>%<neg%>%<imp%>%<p2%>%<pl%>@R.PREF.ne@:in@R.PREF.ne@ # ; ! nenivîsin

LEXICON Accidents_PastStem_în

în%<v%>%<inf%>@D.PREF@:n@D.PREF@ # ; ! nivîsîn
în%<v%>%<inf%>%<obl%>@D.PREF@:na@D.PREF@ # ; ! nivîsîna 
în%<v%>%<pp%>@D.PREF@:%{VSUFP%}@D.PREF@ # ; ! nivîsî

în%<v%>%<perf%>%<pres%>@D.PREF@:@D.PREF@ PersonalSuffixesPerfect ; ! ez nivîsime 
în%<v%>%<neg%>%<perf%>%<pres%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixesPerfect ; ! ez nenivîsime

în%<v%>%<perf%>%<past%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPast ; ! ez nivîsibûm
în%<v%>%<neg%>%<perf%>%<past%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPast ; ! ez nenivîsibûm
în%<v%>%<pis%>@R.PREF.bi@:i%{IEPH%}@R.PREF.bi@ BûnPrs ; ! ez binivîsibim
!! the bí- subjunctive prefix may be omitted for stylisic reasons
în%<v%>%<pis%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPrs ; ! ez nivîsibim ^LR^
în%<v%>%<neg%>%<pis%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPrs ; ! ez nenivîsibim

în%<v%>%<past%>%<cond%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixesIrrealis ; ! ez binivîsama

în%<v%>%<past%>@D.PREF@:@D.PREF@ PersonalSuffixes ; ! ez nivîsim
în%<v%>%<neg%>%<past%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nenivîsim
în%<v%>%<pii%>@R.PREF.di@:@R.PREF.di@ PersonalSuffixes ; ! ez dinivîsim
în%<v%>%<neg%>%<pii%>@R.PREF.nedi@:@R.PREF.nedi@ PersonalSuffixes ; ! ez nedinivîsim

LEXICON Accidents_PresStem_an

an%<v%>%<pri%>@R.PREF.di@@D.PREF.ne@:@R.PREF.di@@D.PREF.ne@ PersonalSuffixes ; ! ez dimam
an%<v%>%<prs%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixes ; ! ez bimam
an%<v%>%<neg%>%<pri%>@R.PREF.na@:@R.PREF.na@ PersonalSuffixes ; ! ez namam
an%<v%>%<neg%>%<prs%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nemam
an%<v%>%<imp%>%<p2%>%<sg%>@R.PREF.bi@:e@R.PREF.bi@ # ; ! bime
an%<v%>%<imp%>%<p2%>%<pl%>@R.PREF.bi@:in@R.PREF.bi@ # ; ! bimin
an%<v%>%<neg%>%<imp%>%<p2%>%<sg%>@R.PREF.ne@:e@R.PREF.ne@ # ; ! neme
an%<v%>%<neg%>%<imp%>%<p2%>%<pl%>@R.PREF.ne@:in@R.PREF.ne@ # ; ! nemin

LEXICON Accidents_PastStem_an

an%<v%>%<inf%>@D.PREF@:n@D.PREF@ # ; ! man
an%<v%>%<inf%>%<obl%>@D.PREF@:na@D.PREF@ # ; ! mana
an%<v%>%<pp%>@D.PREF@:%{VSUFP%}î@D.PREF@ # ; ! manî

an%<v%>%<perf%>%<pres%>@D.PREF@:@D.PREF@ PersonalSuffixesPerfect ; ! ez mamime
an%<v%>%<neg%>%<perf%>%<pres%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixesPerfect ; ! ez nemamime

an%<v%>%<perf%>%<past%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPast ; ! ez mabûm
an%<v%>%<neg%>%<perf%>%<past%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPast ; ! ez nemabûm
an%<v%>%<pis%>@R.PREF.bi@:i%{IEPH%}@R.PREF.bi@ BûnPrs ; ! ez bimabim
!! the bí- subjunctive prefix may be omitted for stylisic reasons
an%<v%>%<pis%>@D.PREF@:i%{IEPH%}@D.PREF@ BûnPrs ; ! ez mabim ^LR^
an%<v%>%<neg%>%<pis%>@R.PREF.ne@:i%{IEPH%}@R.PREF.ne@ BûnPrs ; ! ez nemabim

an%<v%>%<past%>%<cond%>@R.PREF.bi@:@R.PREF.bi@ PersonalSuffixesIrrealis ; ! ez bimama

an%<v%>%<past%>@D.PREF@:@D.PREF@ PersonalSuffixes ; ! ez mam 
an%<v%>%<neg%>%<past%>@R.PREF.ne@:@R.PREF.ne@ PersonalSuffixes ; ! ez nemam
an%<v%>%<pii%>@R.PREF.di@:@R.PREF.di@ PersonalSuffixes ; ! ez dimam
an%<v%>%<neg%>%<pii%>@R.PREF.nedi@:@R.PREF.nedi@ PersonalSuffixes ; ! ez nedimam

LEXICON PersonalSuffixes

%<p1%>%<sg%>:%{VSUF%}im # ;
%<p2%>%<sg%>:%{VSUF%}î # ;
%<p3%>%<sg%>:%{VSUF%}e # ;
%<p1%>%<pl%>:%{VSUF%}in # ;
%<p2%>%<pl%>:%{VSUF%}in # ;
%<p3%>%<pl%>:%{VSUF%}in # ;

LEXICON PersonalSuffixesPerfect

%<p1%>%<sg%>:%{VSUF%}ime # ;
%<p2%>%<sg%>:%{VSUF%}iye # ;
%<p3%>%<sg%>:%{VSUF%}iye # ;
%<p1%>%<pl%>:%{VSUF%}ine # ;
%<p2%>%<pl%>:%{VSUF%}ine # ;
%<p3%>%<pl%>:%{VSUF%}ine # ;

LEXICON PersonalSuffixesIrrealis

%<p1%>%<sg%>:%{VSUF%}ama # ;
%<p2%>%<sg%>:%{VSUF%}ayî # ;
%<p3%>%<sg%>:%{VSUF%}a # ;
%<p1%>%<pl%>:%{VSUF%}ana # ;
%<p2%>%<pl%>:%{VSUF%}ana # ;
%<p3%>%<pl%>:%{VSUF%}ana # ;

LEXICON VerbStems 

ç:ç Accidents_PresStem_ûn  ; ! lm="çûn" :: "to go"
ç:çû Accidents_PastStem_ûn ; ! lm="çûn" :: "to go"

şust:şo Accidents_PresStem_in ; !lm="şustin" :: "to wash"
şust:şust Accidents_PastStem_in ; !lm="şustin" :: "to wash"

axaft:axiv Accidents_PresStem_in ; !lm="axaftin" :: "to speak"
axaft:axaft Accidents_PastStem_in ; !lm="axaftin" :: "to speak"

êşand:êşîn Accidents_PresStem_in ; !lm="êşandin" :: "to hurt"
êşand:êşand Accidents_PastStem_in ; !lm="êşandin" :: "to hurt"

parast:parast Accidents_PresStem_in ; !lm="parastin" :: "to protect"
parast:parast Accidents_PastStem_in ; !lm="parastin" :: "to protect"

firot:firoş Accidents_PresStem_in ; !lm="firotin" :: "to sell" 
firot:firot Accidents_PastStem_in ; !lm="firotin" :: "to sell" 

bîst:bîst Accidents_PresStem_in ; !lm="bîstin" :: "to hear"
bîst:bîst Accidents_PastStem_in ; !lm="bîstin" :: "to hear"

hinart:hinart Accidents_PresStem_in ; !lm="hinartin" :: "to send"
hinart:hinart Accidents_PastStem_in ; !lm="hinartin" :: "to send"

kir:k Accidents_PresStem_in ; !lm="kirin" :: "to do"
kir:kir Accidents_PastStem_in ; !lm="kirin" :: "to do"

mîst:mîst Accidents_PresStem_in ; !lm="mîstin" :: "to urinate"
mîst:mîst Accidents_PastStem_in ; !lm="mîstin" :: "to urinate"

rûnişt:rûnişt Accidents_PresStem_in ; !lm="rûniştin" :: "to sit"
rûnişt:rûnişt Accidents_PastStem_in ; !lm="rûniştin" :: "to sit"

xwar:xw Accidents_PresStem_in ; !lm="xwarin" :: "to eat"
xwar:xwar Accidents_PastStem_in ; !lm="xwarin" :: "to eat"

xwend:xwend Accidents_PresStem_in ; !lm="xwendin" :: "to read"
xwend:xwend Accidents_PastStem_in ; !lm="xwendin" :: "to read"

şand:şand Accidents_PresStem_in ; !lm="şandin" :: "to send"
şand:şand Accidents_PastStem_in ; !lm="şandin" :: "to send"

ket:kev Accidents_PresStem_in ; !lm="ketin" :: "to fall"
ket:ket Accidents_PastStem_in ; !lm="ketin" :: "to fall"

çand:çîn Accidents_PresStem_in ; !lm="çandin" :: ""
çand:çand Accidents_PastStem_in ; !lm="çandin" :: ""

azirand:azirîn Accidents_PastStem_in ; !lm="azirandin" :: "to incite"
azirand:azirand Accidents_PresStem_in ; !lm="azirandin" :: "to incite"

belişand:belişîn Accidents_PastStem_in ; !lm="belişandin" :: "to incite"
belişand:belişand Accidents_PresStem_in ; !lm="belişandin" :: "to incite"

dabiland:dabilîn Accidents_PastStem_in ; !lm="dabilandin" :: "to swallow"
dabiland:dabiland Accidents_PresStem_in ; !lm="dabilandin" :: "to swallow"

fetisand:fetisîn Accidents_PastStem_in ; !lm="fetisandin" :: "to choke"
fetisand:fetisand Accidents_PresStem_in ; !lm="fetisandin" :: "to choke"

mirand:mirîn Accidents_PastStem_in ; !lm="mirandin" :: "to cause to die"
mirand:mirand Accidents_PresStem_in ; !lm="mirandin" :: "to cause to die"

nivîsand:nivîsîn Accidents_PastStem_in ; !lm="nivîsandin" :: "to write"
nivîsand:nivîsand Accidents_PresStem_in ; !lm="nivîsandin" :: "to write"

pesinand:pesinîn Accidents_PastStem_in ; !lm="pesinandin" :: "to pray"
pesinand:pesinand Accidents_PresStem_in ; !lm="pesinandin" :: "to pray"

qedand:qedîn Accidents_PastStem_in ; !lm="qedandin" :: "to finish"
qedand:qedand Accidents_PresStem_in ; !lm="qedandin" :: "to finish"

taşand:taşîn Accidents_PastStem_in ; !lm="taşandin" :: "to educate"
taşand:taşand Accidents_PresStem_in ; !lm="taşandin" :: "to educate"

weşand:weşîn Accidents_PastStem_in ; !lm="weşandin" :: "to publish"
weşand:weşand Accidents_PresStem_in ; !lm="weşandin" :: "to publish"

êşand:êşîn Accidents_PastStem_in ; !lm="êşandin" :: "to hurt"
êşand:êşand Accidents_PresStem_in ; !lm="êşandin" :: "to hurt"

xwest:xwaz Accidents_PresStem_in ; !lm="xwestin" :: "to want"
xwest:xwest Accidents_PastStem_in ; !lm="xwestin" :: "to want"

dît:bîn Accidents_PresStem_in ; !lm="dîtin" :: "to see"
dît:dît Accidents_PastStem_in ; !lm="dîtin" :: "to see"

got:bêj Accidents_PresStem_in ; !lm="gotin" :: "to say"
got:got Accidents_PastStem_in ; !lm="gotin" :: "to say"

nivîs:nivîs Accidents_PresStem_în ; !lm="nivîsîn" :: "to write"
nivîs:nivîsî Accidents_PastStem_în ; !lm="nivîsîn" :: "to write"

tirs:tirs Accidents_PresStem_în ; !lm="tirsîn" :: "to fear"
tirs:tirsî Accidents_PastStem_în ; !lm="tirsîn" :: "to fear"

kir:kir Accidents_PresStem_în ; !lm="kirîn" :: "to bring"
kir:kirî Accidents_PastStem_în ; !lm="kirîn" :: "to bring"

ewt:ew Accidents_PresStem_în ; !lm="ewtîn" :: "to bark"
ewt:ewtî Accidents_PastStem_în ; !lm="ewtîn" :: "to bark"

m:ma Accidents_PresStem_an ; !lm="man" :: "to remain"
m:ma Accidents_PastStem_an ; !lm="man" :: "to remain"

! lm="fikirîn" :: "to think"
! lm="tirsîn" :: "to fear"
! lm="hizirîn" :: "to think"
! lm="pûnijîn" :: "to think"
! lm="ramîn" :: "to think"
! lm="revîn" :: "to escape"
! lm="doşîn" :: "to milk"
! lm="bişirîn" :: "to smile"
! lm="firrîn" :: "to fly"
! lm="kirrîn" :: "to buy"
! lm="mêjîn" :: "to suck"
! lm="bazdan" :: "to escape"
! lm="razan" :: "to go to bed"
! lm="hênan" :: "to bring"
! lm="rahênan" :: "to train"
! lm="meşiyan" :: "to walk"
! lm="jîyan" :: "to live"
! lm="meşîn" :: "to walk"
! lm="zewicîn" :: "se marier"
! lm="xewîn" :: "to sleep"
! lm="werîn" :: "to come"
! lm="pirsîn" :: "to ask"
! lm="peyivîn" :: "to talk"
! lm="westîyan" :: "to be tired"


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Nouns 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

mirov:mirov mirov__n ; ! lm="mirov" :: "man"
xwendevan:xwendevan mirov__n ; ! lm="" :: ""
karker:karker mirov__n ; ! lm="" :: ""
mêr:mêr mirov__n ; ! lm="mêr" :: "man"
doktor:doktor mirov__n ; ! lm="doktor" :: "doctor"
heval:heval mirov__n ; ! lm="heval" :: "friend"
kur:kur mirov__n ; ! lm="" :: ""
xiyar:xiyar mirov__n ; ! lm="xiyar" :: "cucumber"
ap:ap mirov__n ; ! lm="" :: ""
bav:bav mirov__n ; ! lm="" :: ""
çav:çav mirov__n ; ! lm="" :: ""
bel:bel mirov__n ; ! lm="" :: ""
kurd:kurd mirov__n ; ! lm="kurd" :: "kurd"
hogir:hogir mirov__n ; ! lm="" :: ""
gund:gund mirov__n ; ! lm="" :: ""
xwêndekar:xwêndekar mirov__n ; ! lm="" :: ""
endazyar:endazyar mirov__n ; ! lm="" :: ""
ziman:ziman mirov__n ; ! lm="ziman" :: "language"
gotar:gotar mirov__n ; ! lm="" :: ""
şagirt:şagirt mirov__n ; ! lm="" :: ""
nivîskar:nivîskar mirov__n ; ! lm="nivîskar" :: "writer"
ba:ba ba__n; ! lm="ba" :: "wind"
roj:roj roj__n; ! lm="roj" :: "day"
jin:jin roj__n; ! lm="jin" :: "woman"
xanî:xan xan/î__n; ! lm="xanî" :: "house"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Adjectives
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON AdjRoot

nû:nû nû__adj ; ! lm="nû" :: "new"
xur:xur nû__adj ; ! lm="xur" :: "strong"
pêwist:pêwis pêwis/t__adj; ! lm="pêwist" :: "important"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Pronouns 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Numerals
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Numerals

yek:yek yek__num ;  ! lm="yek" :: "one"

di:du şeş__num ; !
sê:sê şeş__num ; !
çar:çar şeş__num ; !
pênc:pênc şeş__num ; !
şeş:şeş şeş__num ; !
heft:heft şeş__num ; !
heşt:heşt şeş__num ; !
neh:neh şeş__num ; !
deh:deh şeş__num ; !
yanzdeh:yanzdeh şeş__num ; !
dwanzdeh:dwanzdeh şeş__num ; !
sêzdeh:sêzdeh şeş__num ; !
çardeh:çardeh şeş__num ; !
panzdeh:panzdeh şeş__num ; !
şanzdeh:şanzdeh şeş__num ; !
hivdeh:hivdeh şeş__num ; !
hijdeh:hijdeh şeş__num ; !
nozdeh:nozdeh şeş__num ; !


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Prepositions
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prepositions

cem:cem Prep  ; ! lm="cem" :: "near to"
ba:ba Prep  ; ! lm="ba" :: "near to"
ji:ji Prep ; ! lm="ji" :: "from"
ji% bo:ji% bo Prep ; ! lm="ji bo" :: "in order to"
ber:ber Prep  ; ! lm="ber" :: "in front of"
berî:berî Prep  ; ! lm="berî" :: "before"
derveyî:derveyî Prep ; ! lm="berî" :: "outside of"
piştî:piştî Prep ; ! lm="piştî" :: "after"
ser:ser Prep ; ! lm="ser" :: "on"
ta:ta Prep ; ! lm="ta" :: "until"
tevî:tevî Prep ; ! lm="tevî" :: "along with"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Conjunctions
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Conjunctions

û:û CC ; ! lm="û" :: "and"
çaxê% ku:çaxê% ku CS ; ! lm="çaxê ku" :: "when"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Punctuation
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Punctuation

.%<sent%>:. # ;
!%<sent%>:! # ;
?%<sent%>:? # ;
...%<sent%>:... # ;
